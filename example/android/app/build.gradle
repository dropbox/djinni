apply plugin: 'com.android.application'

android {
    compileSdkVersion 23
    buildToolsVersion "26.0.0"

    defaultConfig {
        applicationId "com.dropbox.textsort"
        minSdkVersion 15
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    sourceSets {
        main {
            java.srcDirs = ['src', '../../handwritten-src/java', '../../generated-src/java']
            jni.srcDirs = []
            jniLibs.srcDirs = ['libs']
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.google.code.findbugs:jsr305:3.0.0'
}

task ndkBuild(type: Exec) {
    File ndkDir = project.android.ndkDirectory
    def ndkBuildExecutable = new File(ndkDir, 'ndk-build')

    doFirst {
        if (ndkDir == null) {
            def gradle_project_root = project.rootProject.rootDir
            throw new GradleException("NDK is not configured. Make sure there is a local.properties " +
                    "file with an ndk.dir entry in the directory ${gradle_project_root}.")
        }

        if (!ndkBuildExecutable.exists()) {
            throw new GradleException("Could not find ndk-build. The configured NDK directory ${ndkDir} may not be correct.")
        }
    }

    String MainDirectory = System.getProperty("user.dir") + '/app/'
    environment("NDK_PROJECT_PATH", MainDirectory)
    environment("GYP_CONFIGURATION", "Release")
    commandLine ndkBuildExecutable
}

tasks.withType(JavaCompile) {
    compileTask -> compileTask.dependsOn ndkBuild
}

